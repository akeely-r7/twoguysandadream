buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.2.1.RELEASE"
        classpath 'org.flywaydb:flyway-gradle-plugin:3.1'
        classpath 'mysql:mysql-connector-java:5.1.34'
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'idea'

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:1.1.3.RELEASE'
    }
}

repositories {

    mavenLocal()
    jcenter()
}

bootRepackage {
    mainClass = "com.twoguysandadream.app.Application"
}

configurations {
    all*.exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
}

/**
 * Temporary workaround to use latest Spring security. This should go away with Spring Boot 1.3
 */
configurations.all {
    resolutionStrategy {
        eachDependency {
            if (it.requested.group == 'org.springframework.security') {
                it.useVersion '4.0.2.RELEASE'
            }
        }
    }
}

sourceSets {
    ft
    uat
}

task ft(type: Test, group: 'Verification', description: 'Runs the Functional Tests') {
    testClassesDir = sourceSets.ft.output.classesDir
    classpath = sourceSets.ft.runtimeClasspath
}

task uat(type: Test, group: 'Verification', description: 'Runs the User Acceptance Tests') {
    testClassesDir = sourceSets.uat.output.classesDir
    classpath = sourceSets.uat.runtimeClasspath
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "mysql:mysql-connector-java"
    compile "com.mchange:c3p0"
    compile "org.springframework:spring-jdbc"
    compile "org.springframework.security:spring-security-openid"

    testCompile "junit:junit"
    testCompile "org.springframework:spring-test"
    testCompile "org.hsqldb:hsqldb"
    testCompile "commons-io:commons-io"

    uatCompile "org.codehaus.groovy:groovy-all"
    uatCompile "info.cukes:cucumber-groovy:1.2.2"
    uatCompile "info.cukes:cucumber-junit:1.2.2"
    uatCompile "junit:junit"
    uatCompile "org.flywaydb:flyway-core"
    uatCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7"
    uatCompile "mysql:mysql-connector-java"
    uatCompile "com.mchange:c3p0"
    uatCompile "org.springframework:spring-jdbc"
    uatCompile "org.springframework:spring-web"
    uatCompile "org.apache.commons:commons-dbcp2"
}

idea {
    module {
        scopes.TEST.plus += [configurations.uatCompile, configurations.ftCompile]
    }
}
