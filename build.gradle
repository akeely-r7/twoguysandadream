buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE"
        classpath 'org.flywaydb:flyway-gradle-plugin:4.0'
        classpath 'mysql:mysql-connector-java:5.1.34'
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.0.3"
    id "io.spring.dependency-management" version "0.6.0.RELEASE"
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'idea'

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:2.0.7.RELEASE'
    }
}

repositories {

    mavenLocal()
    jcenter()
}

bootRepackage {
    mainClass = "com.twoguysandadream.app.Application"
}

bootRun {
    environment 'spring.profiles.active', 'dev'
    addResources = true
}

bootRun.doFirst {
    file("${project.buildDir}/logs").mkdirs()
}

flyway {
    url = 'jdbc:mysql://localhost:3306/auction_prod2'
    user = 'root'
    schemas = ['auction_prod2']
    baselineDescription = 'Production import'
}

sourceSets {
    ft
    uat
}

task ft(type: Test, group: 'Verification', description: 'Runs the Functional Tests') {
    testClassesDir = sourceSets.ft.output.classesDir
    classpath = sourceSets.ft.runtimeClasspath
}

task uat(type: Test, group: 'Verification', description: 'Runs the User Acceptance Tests') {
    testClassesDir = sourceSets.uat.output.classesDir
    classpath = sourceSets.uat.runtimeClasspath
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    compile "org.springframework.boot:spring-boot-devtools"
    compile "mysql:mysql-connector-java"
    compile "com.mchange:c3p0"
    compile "org.springframework:spring-jdbc"
    compile "org.springframework.security:spring-security-openid"
    compile "com.google.guava:guava"
    compile "org.flywaydb:flyway-core:4.0.3"

    testCompile "junit:junit"
    testCompile "org.springframework:spring-test"
    testCompile "org.hsqldb:hsqldb"
    testCompile "commons-io:commons-io"
    testCompile "org.mockito:mockito-core"

    uatCompile "org.codehaus.groovy:groovy-all"
    uatCompile "info.cukes:cucumber-groovy:1.2.2"
    uatCompile "info.cukes:cucumber-junit:1.2.2"
    uatCompile "junit:junit"
    uatCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7"
    uatCompile "mysql:mysql-connector-java"
    uatCompile "com.mchange:c3p0"
    uatCompile "org.springframework:spring-jdbc"
    uatCompile "org.springframework:spring-web"
    uatCompile "org.apache.commons:commons-dbcp2"
}

idea {
    module {
        scopes.TEST.plus += [configurations.uatCompile, configurations.ftCompile]
    }
}
